buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'java-library'
}

apply plugin: 'com.bmuschko.nexus'

group 'com.github.fbaro'
version '1.0.2'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    api 'com.github.ben-manes.caffeine:caffeine:2.9.0'
    api 'com.google.code.findbugs:jsr305:3.0.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'com.google.guava:guava:30.1.1-jre'
}

test {
    useJUnit {
        excludeCategories 'com.github.fbaro.tcache.SlowTests'
    }
}

task slowTest(type: Test) {
    description = 'Runs slow tests.'
    group = 'verification'

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    shouldRunAfter test
}

modifyPom {
    project {
        name 'tcache'
        description 'A cache for time series data'
        url 'https://github.com/fbaro/tcache'
        inceptionYear '2020'

        scm {
            url 'https://github.com/fbaro/tcache'
            connection 'scm:git:https://github.com/fbaro/tcache.git'
            developerConnection 'scm:git:git@github.com:fbaro/tcache.git'
        }

        licenses {
            license {
                name 'GNU LESSER GENERAL PUBLIC LICENSE, Version 3'
                url 'https://www.gnu.org/licenses/lgpl-3.0.txt'
            }
        }

        developers {
            developer {
                id 'fbaro'
                name 'Flavio Baronti'
                email 'f.baro@libero.it'
            }
        }
    }
}
